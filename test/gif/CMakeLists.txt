#######################################################################
# Copyright (C) 2019 Xueyi Yao
#
# This file is part of VPixels.
#
# VPixels is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
# 
# VPixels is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with VPixels.  If not, see <https://www.gnu.org/licenses/>.
#######################################################################

# test libvpgif
message(STATUS "Configuring test/gif/")
if(NOT ${CPPUNIT_FOUND})
  message(STATUS "libvpgif tests skipped")
  return()
endif()

# path to /src/gif
include_directories(${PROJECT_SOURCE_DIR}/src/gif)

# path to CppUnit lib
link_directories(${CPPUNIT_LIBRARY_DIRS})

#
# target: ListGifComponents
#
add_executable(ListGifComponents EXCLUDE_FROM_ALL ListGifComponents.cpp)
target_link_libraries(ListGifComponents vpgif)

#
# target: GifEncoderDecoderTest
#
add_executable(GifEncoderDecoderTest EXCLUDE_FROM_ALL
               GifCodeWriterTest.cpp GifCodeReaderTest.cpp GifStringTableTest.cpp
               GifEncoderTest.cpp GifDecoderTest.cpp GifEncoderDecoderTest.cpp
               ${PROJECT_SOURCE_DIR}/test/UnitTestMain.cpp)

target_compile_options(GifEncoderDecoderTest PUBLIC ${CPPUNIT_CFLAGS})

#
# target: GifComponentsTest
#
add_executable(GifComponentsTest EXCLUDE_FROM_ALL
               GifHeaderTest.cpp GifBlockIOTest.cpp GifScreenDescriptorTest.cpp
               GifColorTableTest.cpp GifGraphicsControlExtTest.cpp GifImageDataTest.cpp
               GifImageDescriptorTest.cpp GifApplicationExtTest.cpp GifCommentExtTest.cpp
               GifComponentTest.cpp GifComponentVecUtilTest.cpp GifImageVecBuilderTest.cpp
               GifImageTest.cpp GifImplTest.cpp GifTest.cpp
               ${PROJECT_SOURCE_DIR}/test/UnitTestMain.cpp)

target_compile_options(GifComponentsTest PUBLIC ${CPPUNIT_CFLAGS})

# common to GifEncoderDecoderTest GifComponentsTest
set_target_properties(GifEncoderDecoderTest GifComponentsTest
                      PROPERTIES LINK_LIBRARIES "vpgif;${CPPUNIT_LIBRARIES}")

#
# target: check-gif, run gif tests
#
add_custom_target(check-gif DEPENDS check-endecoder check-gifcomponents)

#
# target: check-endecoder, run GifEncoderDecoderTest
#
add_custom_target(check-endecoder DEPENDS GifEncoderDecoderTest
                  COMMENT "Encoder Decoder tests"
                  COMMAND GifEncoderDecoderTest)

#
# target: check-gifcomponents, run GifComponentsTest
#
add_custom_target(check-gifcomponents
                  COMMENT "GIF components tests"
                  DEPENDS GifComponentsTest empty.gif export_exist.gif header_only.gif
                  COMMAND ${CMAKE_COMMAND} -E remove -f export_new.gif
                  COMMAND GifComponentsTest)

# prepare for running GifComponentsTest
# generate files empty.gif, export_exist.gif and header_only.gif
# export_new.gif is generated by GifComponentsTest, list it here as OUTPUT and
# hence will be removed by target clean
add_custom_command(COMMENT "Prepare GIF components tests"
                   OUTPUT empty.gif export_exist.gif header_only.gif export_new.gif
                   COMMAND ${CMAKE_COMMAND} -E touch empty.gif export_exist.gif
                   COMMAND ${CMAKE_COMMAND} -E echo "GIF89a" > header_only.gif)
