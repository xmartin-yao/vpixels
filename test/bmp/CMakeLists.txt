#######################################################################
# Copyright (C) 2019 Xueyi Yao
#
# This file is part of VPixels.
#
# VPixels is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
# 
# VPixels is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with VPixels.  If not, see <https://www.gnu.org/licenses/>.
#######################################################################

# test libvpbmp
message(STATUS "Configuring test/bmp/")
if(NOT ${CPPUNIT_FOUND})
  message(STATUS "libvpbmp tests skipped")
  return()
endif()

# path to /src/bmp
include_directories(${PROJECT_SOURCE_DIR}/src/bmp)

# path to CppUnit lib
link_directories(${CPPUNIT_LIBRARY_DIRS})

#
# target: BmpTest
#
add_executable(BmpTest EXCLUDE_FROM_ALL
               BmpInfoTest.cpp BmpInfo1BitTest.cpp BmpInfo4BitTest.cpp
               BmpInfo8BitTest.cpp BmpInfo24BitTest.cpp BmpFileHeaderTest.cpp
               BmpInfoHeaderTest.cpp BmpColorTableTest.cpp BmpImageDataTest.cpp
               BmpImplTest.cpp BmpTest.cpp
               ${PROJECT_SOURCE_DIR}/test/UnitTestMain.cpp)

target_compile_options(BmpTest PUBLIC ${CPPUNIT_CFLAGS})
target_link_libraries(BmpTest vpbmp ${CPPUNIT_LIBRARIES})

#
# target: check-bmp
#
add_custom_target(check-bmp
                  COMMENT "BMP tests"
                  DEPENDS BmpTest empty.bmp export_exist.bmp
                  COMMAND ${CMAKE_COMMAND} -E remove -f export_new.bmp
                  COMMAND BmpTest) # use target name to invoke it, don't need path

# prepare for running BmpTest
# generate files empty.bmp and export_exist.bmp
# export_new.bmp is generated by BmpTest, list it here as OUTPUT and hence will be
# removed by target clean
add_custom_command(COMMENT "Prepare BMP tests"
                   OUTPUT empty.bmp export_exist.bmp export_new.bmp
                   COMMAND ${CMAKE_COMMAND} -E touch empty.bmp export_exist.bmp)
